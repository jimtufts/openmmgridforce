CMAKE_MINIMUM_REQUIRED(VERSION 3.16)
PROJECT(OpenMMGridForcePlugin CUDA CXX)

# Find CUDA
FIND_PACKAGE(CUDAToolkit REQUIRED)
IF(CUDAToolkit_FOUND)
    MESSAGE(STATUS "Found CUDA ${CUDAToolkit_VERSION} at ${CUDAToolkit_ROOT}")
    SET(GRIDFORCE_BUILD_CUDA_LIB ON CACHE BOOL "Build implementation for CUDA")
ELSE()
    SET(GRIDFORCE_BUILD_CUDA_LIB OFF CACHE BOOL "Build implementation for CUDA")
ENDIF()

# Use CONDA_PREFIX environment variable to find OpenMM
SET(OPENMM_DIR $ENV{CONDA_PREFIX} CACHE PATH "Where OpenMM is installed")
MESSAGE(STATUS "Looking for OpenMM at ${OPENMM_DIR}")

# Add OpenMM to include and library paths
INCLUDE_DIRECTORIES("${OPENMM_DIR}/include")
LINK_DIRECTORIES("${OPENMM_DIR}/lib" "${OPENMM_DIR}/lib/plugins")

# Check if we can find essential OpenMM headers
FIND_FILE(OPENMM_CONTEXT_H "openmm/Context.h" PATHS "${OPENMM_DIR}/include" NO_DEFAULT_PATH)
IF(NOT OPENMM_CONTEXT_H)
    MESSAGE(FATAL_ERROR "Could not find OpenMM headers in ${OPENMM_DIR}/include")
ENDIF()

# Specify the C++ version we are building for.
SET(CMAKE_CXX_STANDARD 11)
SET(CMAKE_CUDA_STANDARD 11)
SET(CMAKE_CUDA_ARCHITECTURES 89)

# Set flags for linking on mac
IF(APPLE)
    SET(CMAKE_INSTALL_NAME_DIR "@rpath")
    SET(EXTRA_COMPILE_FLAGS "-msse2 -stdlib=libc++")
ENDIF(APPLE)

# Select where to install
IF(${CMAKE_INSTALL_PREFIX_INITIALIZED_TO_DEFAULT})
    IF(WIN32)
        SET(CMAKE_INSTALL_PREFIX "$ENV{ProgramFiles}/OpenMM" CACHE PATH "Where to install the plugin" FORCE)
    ELSE(WIN32)
        SET(CMAKE_INSTALL_PREFIX "${OPENMM_DIR}" CACHE PATH "Where to install the plugin" FORCE)
    ENDIF(WIN32)
ENDIF(${CMAKE_INSTALL_PREFIX_INITIALIZED_TO_DEFAULT})

# The source is organized into subdirectories, but we handle them all from
# this CMakeLists file rather than letting CMake visit them as SUBDIRS.
SET(GRIDFORCE_PLUGIN_SOURCE_SUBDIRS openmmapi)

# Set the library name
SET(GRIDFORCE_LIBRARY_NAME OpenMMGridForce)
SET(SHARED_GRIDFORCE_TARGET ${GRIDFORCE_LIBRARY_NAME})

# These are all the places to search for header files which are to be part of the API.
SET(API_INCLUDE_DIRS "openmmapi/include" "openmmapi/include/internal")

# Locate header files.
SET(API_INCLUDE_FILES)
FOREACH(dir ${API_INCLUDE_DIRS})
    FILE(GLOB fullpaths ${dir}/*.h)
    SET(API_INCLUDE_FILES ${API_INCLUDE_FILES} ${fullpaths})
ENDFOREACH(dir)

# Collect up source files
SET(SOURCE_FILES) # empty
SET(SOURCE_INCLUDE_FILES)
FOREACH(subdir ${GRIDFORCE_PLUGIN_SOURCE_SUBDIRS})
    FILE(GLOB src_files  ${CMAKE_CURRENT_SOURCE_DIR}/${subdir}/src/*.cpp)
    FILE(GLOB incl_files ${CMAKE_CURRENT_SOURCE_DIR}/${subdir}/src/*.h)
    SET(SOURCE_FILES         ${SOURCE_FILES}         ${src_files})   #append
    SET(SOURCE_INCLUDE_FILES ${SOURCE_INCLUDE_FILES} ${incl_files})

    ## Make sure we find these locally before looking in OpenMM/include if
    ## OpenMM was previously installed there.
    INCLUDE_DIRECTORIES(BEFORE ${CMAKE_CURRENT_SOURCE_DIR}/${subdir}/include)
ENDFOREACH(subdir)

# Create the library.
ADD_LIBRARY(${SHARED_GRIDFORCE_TARGET} SHARED ${SOURCE_FILES} ${SOURCE_INCLUDE_FILES} ${API_INCLUDE_FILES})
SET_TARGET_PROPERTIES(${SHARED_GRIDFORCE_TARGET} 
    PROPERTIES COMPILE_FLAGS "-DALGDOCK_BUILDING_SHARED_LIBRARY ${EXTRA_COMPILE_FLAGS}"
    LINK_FLAGS "${EXTRA_COMPILE_FLAGS}")
TARGET_LINK_LIBRARIES(${SHARED_GRIDFORCE_TARGET} OpenMM)
INSTALL_TARGETS(/lib RUNTIME_DIRECTORY /lib ${SHARED_GRIDFORCE_TARGET})

# install headers 
FILE(GLOB API_ONLY_INCLUDE_FILES "openmmapi/include/*.h")
INSTALL(FILES ${API_ONLY_INCLUDE_FILES} DESTINATION include)
FILE(GLOB API_ONLY_INCLUDE_FILES_OPENMM "openmmapi/include/internal/*.h")
INSTALL(FILES ${API_ONLY_INCLUDE_FILES_OPENMM} DESTINATION include/internal)

# Enable testing
ENABLE_TESTING()

# Add make check target
ADD_CUSTOM_TARGET(check
    COMMAND ${CMAKE_CTEST_COMMAND}
    DEPENDS PythonInstall)

# Build the implementations for different platforms
ADD_SUBDIRECTORY(platforms/common)
ADD_SUBDIRECTORY(platforms/reference)

IF(GRIDFORCE_BUILD_CUDA_LIB)
    ADD_SUBDIRECTORY(platforms/cuda)
ENDIF(GRIDFORCE_BUILD_CUDA_LIB)

# Build the Python API
FIND_PROGRAM(PYTHON_EXECUTABLE python)
FIND_PROGRAM(SWIG_EXECUTABLE swig)
IF(PYTHON_EXECUTABLE AND SWIG_EXECUTABLE)
    SET(GRIDFORCE_BUILD_PYTHON_WRAPPERS ON CACHE BOOL "Build wrappers for Python")
ELSE(PYTHON_EXECUTABLE AND SWIG_EXECUTABLE)
    SET(GRIDFORCE_BUILD_PYTHON_WRAPPERS OFF CACHE BOOL "Build wrappers for Python")
ENDIF(PYTHON_EXECUTABLE AND SWIG_EXECUTABLE)
IF(GRIDFORCE_BUILD_PYTHON_WRAPPERS)
    ADD_SUBDIRECTORY(python)
ENDIF(GRIDFORCE_BUILD_PYTHON_WRAPPERS)
