#---------------------------------------------------
# OpenMM GridForce Plugin CUDA Platform
#----------------------------------------------------

SET(OPENMM_GRIDFORCE_CUDA_LIBRARY_NAME OpenMMGridForceCUDA)
SET(SHARED_TARGET ${OPENMM_GRIDFORCE_CUDA_LIBRARY_NAME})

# These are all the places to search for header files
SET(API_INCLUDE_DIRS "${CMAKE_CURRENT_SOURCE_DIR}/include" "${CMAKE_CURRENT_SOURCE_DIR}/include/internal")

# Locate header files.
SET(API_INCLUDE_FILES)
FOREACH(dir ${API_INCLUDE_DIRS})
    FILE(GLOB fullpaths ${dir}/*.h)
    SET(API_INCLUDE_FILES ${API_INCLUDE_FILES} ${fullpaths})
ENDFOREACH(dir)

# Generate kernel source from .cu file
FILE(GLOB KERNEL_FILES "src/kernels/*.cu")
SET(KERNEL_SOURCE_CLASS CudaGridForceKernelSources)
SET(KERNELS_CPP ${CMAKE_CURRENT_BINARY_DIR}/src/${KERNEL_SOURCE_CLASS}.cpp)
SET(KERNELS_H "${CMAKE_CURRENT_SOURCE_DIR}/src/${KERNEL_SOURCE_CLASS}.h")
FILE(MAKE_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/src)

ADD_CUSTOM_COMMAND(OUTPUT ${KERNELS_CPP}
    COMMAND ${CMAKE_COMMAND}
        -D KERNEL_FILES="${KERNEL_FILES}"
        -D KERNELS_CPP=${KERNELS_CPP}
        -D KERNELS_H=${KERNELS_H}
        -D KERNEL_SOURCE_CLASS=${KERNEL_SOURCE_CLASS}
        -D KERNELS_DIR=${CMAKE_CURRENT_SOURCE_DIR}/src/kernels
        -D CUDA_KERNEL=TRUE
        -P ${CMAKE_SOURCE_DIR}/platforms/cuda/EncodeCudaFiles.cmake
    DEPENDS ${KERNEL_FILES}
    COMMENT "Generating ${KERNEL_SOURCE_CLASS}.cpp"
)
SET_SOURCE_FILES_PROPERTIES(${KERNELS_CPP} PROPERTIES GENERATED TRUE)
ADD_CUSTOM_TARGET(CudaKernels DEPENDS ${KERNELS_CPP})

# Collect up source files
SET(SOURCE_FILES) # empty
SET(SOURCE_INCLUDE_FILES)

FILE(GLOB src_files ${CMAKE_CURRENT_SOURCE_DIR}/src/*.cpp)
FILE(GLOB incl_files ${CMAKE_CURRENT_SOURCE_DIR}/src/*.h)
SET(SOURCE_FILES ${SOURCE_FILES} ${src_files} ${KERNELS_CPP})
SET(SOURCE_INCLUDE_FILES ${SOURCE_INCLUDE_FILES} ${incl_files})

INCLUDE_DIRECTORIES(BEFORE ${CMAKE_CURRENT_SOURCE_DIR}/include)
INCLUDE_DIRECTORIES(BEFORE ${CMAKE_CURRENT_SOURCE_DIR}/../common/include)
INCLUDE_DIRECTORIES(BEFORE ${CMAKE_SOURCE_DIR}/platforms/cuda/include)
INCLUDE_DIRECTORIES(BEFORE ${CMAKE_SOURCE_DIR}/platforms/cuda/src)
INCLUDE_DIRECTORIES(BEFORE ${CMAKE_BINARY_DIR}/platforms/cuda/src)
INCLUDE_DIRECTORIES(BEFORE ${CMAKE_SOURCE_DIR}/platforms/common/include)
INCLUDE_DIRECTORIES(BEFORE ${CMAKE_BINARY_DIR}/platforms/common/src)
INCLUDE_DIRECTORIES(BEFORE ${CMAKE_CURRENT_BINARY_DIR}/../common/src)
INCLUDE_DIRECTORIES(BEFORE ${CMAKE_CURRENT_BINARY_DIR}/../common/include)
INCLUDE_DIRECTORIES(BEFORE ${CMAKE_SOURCE_DIR}/openmmapi/include)
INCLUDE_DIRECTORIES(BEFORE ${CUDAToolkit_INCLUDE_DIRS})

# Create the library
ADD_LIBRARY(${SHARED_TARGET} SHARED ${SOURCE_FILES} ${SOURCE_INCLUDE_FILES} ${API_INCLUDE_FILES})
ADD_DEPENDENCIES(${SHARED_TARGET} CudaKernels)

TARGET_LINK_LIBRARIES(${SHARED_TARGET} CUDA::cuda_driver CUDA::cudart OpenMM OpenMMCUDA OpenMMGridForce)

SET_TARGET_PROPERTIES(${SHARED_TARGET} PROPERTIES
    CUDA_ARCHITECTURES 89
    POSITION_INDEPENDENT_CODE ON
    COMPILE_FLAGS "-DOPENMM_BUILDING_SHARED_LIBRARY ${EXTRA_COMPILE_FLAGS}"
    LINK_FLAGS "${EXTRA_COMPILE_FLAGS}")

# Set CUDA compilation flags for compatibility with older drivers
# Generate PTX for older CUDA versions to ensure driver compatibility
SET(CMAKE_CUDA_FLAGS "${CMAKE_CUDA_FLAGS} -gencode arch=compute_89,code=sm_89")

INSTALL(TARGETS ${SHARED_TARGET} DESTINATION ${CMAKE_INSTALL_PREFIX}/lib/plugins)

# Ensure that links to the main CUDA library will be resolved.
IF (APPLE)
    SET(CUDA_LIBRARY libOpenMMCUDA.dylib)
    INSTALL(CODE "EXECUTE_PROCESS(COMMAND install_name_tool -change ${CUDA_LIBRARY} @loader_path/${CUDA_LIBRARY} ${CMAKE_INSTALL_PREFIX}/lib/plugins/lib${SHARED_TARGET}.dylib)")
ENDIF (APPLE)
